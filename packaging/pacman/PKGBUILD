# Maintainer: Andrei Costescu <andrei@costescu.no>

# shellcheck shell=bash

pkgname=go-check-updates-git
_pkgname="${pkgname%-git}"
pkgver=9879e6b
pkgrel=1
pkgdesc="Get pending updates through API"
arch=("any")
url="https://github.com/cosandr/go-check-updates"
license=("MIT")
provides=("${_pkgname}")
conflicts=("${_pkgname}")
depends=("pacman-contrib" "curl")
optdepends=('pikaur: AUR support' 'yay: AUR support' 'paru: AUR support')
makedepends=("git" "go")
source=("git+$url")
md5sums=("SKIP")
install="${_pkgname}.install"

pkgver() {
    cd "${_pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
    cd "${_pkgname}"
    _args="--pkg-name "${_pkgname}" --systemd-path ./ --hook-path ./ --tmp-path ./tmp --no-log --no-cache --no-watch"
    ./setup.sh install systemd $_args
    ./setup.sh install hook $_args
    go mod vendor
    go build -o "${_pkgname}"
}

package() {
    cd "${_pkgname}"
    install -d "${pkgdir}/usr/lib/systemd/system"
    install -d "${pkgdir}/usr/share/libalpm/hooks"
    install -Dm 755 "${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
    install -m 644 "${_pkgname}.service" "${pkgdir}/usr/lib/systemd/system/"
    install -m 644 "${_pkgname}.socket" "${pkgdir}/usr/lib/systemd/system/"
    install -m 644 "${_pkgname}.hook" "${pkgdir}/usr/share/libalpm/hooks/"
    install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
